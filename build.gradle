// tag::versions[]
buildscript {
	dependencies {
		apply from: "versions.gradle"

		def depMgmtVersion = versions['dependency.management.plugin.version']

		classpath "io.spring.gradle:dependency-management-plugin:${depMgmtVersion}"
		classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.6.1'
		classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.16'
        classpath 'org.ysb33r.gradle:grolifant:0.12.1'
		classpath 'com.github.node-gradle:gradle-node-plugin:1.4.0'
		classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.5-rc1'
		classpath "net.ltgt.gradle:gradle-apt-plugin:0.10"

		// asciidoc java 9 issues: https://github.com/asciidoctor/asciidoctorj/issues/515
		classpath 'org.jruby:jruby-complete:9.1.17.0'

		classpath 'com.bmuschko:gradle-docker-plugin:3.2.8'
	}
}

allprojects {
    configurations.all {
        resolutionStrategy {
            force 'org.asciidoctor:asciidoctorj-groovy-dsl:2.0.2'
        }
    }
}

wrapper {
	gradleVersion = '4.7'
}

version = BUILD_VERSION_PREFIX + "-cropster-" + FORK_VERSION
group = GROUP_ID

// note that a production build creates javadoc, sources jars and signatures undesired during development
// for performance reasons
def isProductionBuild = !project.version.endsWith('SNAPSHOT')
ext {
    publishingUsername = project.findProperty('io.crnk.repository.username')
    publishingPassword = project.findProperty('io.crnk.repository.password')
    publishingRepository = project.findProperty('io.crnk.repository.maven')
    publishingSnapshotRepository = project.findProperty('io.crnk.repository.maven-snapshots')
}

if (JavaVersion.current().isJava8Compatible()) {
	allprojects {
		tasks.withType(Javadoc) {
			options.addStringOption('Xdoclint:none', '-quiet')
		}
	}
}

gradle.beforeProject { Project project ->
	project.with {
		apply plugin: "eclipse"

		apply plugin: 'maven'

		def docs = project.name == 'crnk-documentation'
		def ui = project.name == 'crnk-ui'
		def testProject = project.name == 'crnk-test'
		def legacyBraveProject = project.name == 'crnk-brave'
		def bom = project.name == 'crnk-bom'
		def typescript = project.name == 'crnk-gen-typescript'
		def examples = project.name.contains('example')

		if (!docs) {
			apply plugin: 'java'

			sourceCompatibility = 1.8
			targetCompatibility = 1.8

			if (!bom) {


				dependencies {
					testCompile group: 'junit', name: 'junit', version: '4.12'
					testCompile group: 'org.mockito', name: 'mockito-core', version: '1.10.19'
					testCompile group: 'org.assertj', name: 'assertj-core', version: '2.2.0'
				}

				test {
					testLogging {
						exceptionFormat = 'full'
					}
				}

				apply plugin: 'io.spring.dependency-management'
				dependencyManagement {
					imports {
						mavenBom 'io.projectreactor:reactor-bom:Bismuth-RELEASE'
						mavenBom 'org.glassfish.jersey:jersey-bom:2.26'
					}
					dependencies {
						dependency 'com.fasterxml.jackson.core:jackson-databind:2.9.5'
						dependency 'com.fasterxml.jackson.core:jackson-annotations:2.9.5'
						dependency 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.5'
						dependency 'com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.9.5'
						dependency 'com.fasterxml.jackson.jaxrs:jackson-jaxrs-base:2.9.5'
						dependency 'com.fasterxml.jackson.core:jackson-core:2.9.5'
					}
				}
			}
		}

		if (!docs && !examples && !testProject && !legacyBraveProject) {
			apply plugin: "jacoco"
			jacoco {
				toolVersion = "0.7.6.201602180812"
			}

			rootProject.tasks.jacocoMerge.executionData tasks.withType(Test)
			rootProject.tasks.jacocoRootReport.additionalSourceDirs files(sourceSets.main.allSource.srcDirs)

			def sourceDirs = rootProject.tasks.jacocoRootReport.sourceDirectories
			def classDirs = rootProject.tasks.jacocoRootReport.classDirectories

			def mainOutput = files(files(sourceSets.main.output).collect {
				fileTree(dir: it, exclude: '**/legacy/**')
			})

			if (sourceDirs == null) {
				rootProject.tasks.jacocoRootReport.sourceDirectories = files(sourceSets.main.allSource.srcDirs)
				rootProject.tasks.jacocoRootReport.classDirectories = mainOutput

			} else {
				rootProject.tasks.jacocoRootReport.sourceDirectories = sourceDirs.plus(files(sourceSets.main.allSource.srcDirs))
				rootProject.tasks.jacocoRootReport.classDirectories = classDirs.plus(mainOutput)
			}



			jacocoTestReport {
				reports {
					xml.enabled = true // coveralls plugin depends on xml format report
					html.enabled = true
				}
			}
		}

		apply plugin: 'maven-publish'

		if (!docs && !examples) {

			task sourcesJar(type: Jar) {
				from sourceSets.main.allSource
				classifier = 'sources'
			}

			task javadocJar(type: Jar, dependsOn: 'javadoc') {
				from javadoc.destinationDir
				classifier = 'javadoc'
			}

			// Create the pom configuration:
			def pomConfig = {
				licenses {
					license {
						name "The Apache Software License, Version 2.0"
						url "http://www.apache.org/licenses/LICENSE-2.0.txt"
						distribution "repo"
					}
				}
				developers {
					developer {
						id 'crnk.io'
						name 'crnk.io'
						email 'info@crnk.io'
					}
				}

				scm {
					url "https://github.com/crnk-project/crnk-framework"
				}
			}

            publishing {
			    if (!bom && !typescript) {
					publications {
						mavenJava(MavenPublication) {
							from components.java

							artifact sourcesJar {
								classifier "sources"
							}

							artifact javadocJar {
								classifier "javadoc"
							}

							groupId GROUP_ID
							artifactId project.name
							version project.version

							pom.withXml {
								def root = asNode()
								root.appendNode('description', 'JSON API with Crnk')
								root.appendNode('name', 'Crnk')
								root.appendNode('url', 'http://wwww.crnk.io')
								root.children().last() + pomConfig
							}
						}
					}
				}
                repositories {
                    maven {
                       credentials {
                           username = publishingUsername
                           password = publishingPassword
                       }
                       url = project.version.endsWith('SNAPSHOT') ? publishingSnapshotRepository : publishingRepository
                    }
                }
            }
			def releaseBuild =  project.hasProperty('stable')
		}

	}
}

// jacoco setup
apply plugin: "jacoco"

def publishedProjects = subprojects.findAll {
	it.name != 'crnk-documentation' && it.name != 'crnk-ui' && it.name != 'crnk-test' && !it.name.contains('example')
}

task jacocoMerge(type: JacocoMerge) {
	destinationFile = new File(project.buildDir, 'jacoco/test.exec')
	doFirst {
		executionData = files(executionData.findAll { it.exists() })
	}
	for (publishedProject in publishedProjects) {
		dependsOn publishedProject.path + ":check"
	}
}


task jacocoRootReport(type: JacocoReport, group: 'Coverage reports') {
	description = 'Generates an aggregate report from all subprojects'

	dependsOn tasks.jacocoMerge

	executionData tasks.jacocoMerge.destinationFile

	reports {
		html.enabled = true // human readable
		xml.enabled = true // required by coveralls
	}

}


